********** compound composition, two premises
<swan --> swimmer>. %0.90% 
// Swan is a type of swimmer.
<swan --> bird>. %0.80% 
// Swan is a type of bird.
1
 OUT: <swan --> (|,bird,swimmer)>. %0.98;0.96% 
// Swan is a type of bird or a type of swimmer. 
 OUT: <swan --> (&,bird,swimmer)>. %0.72;0.84% 
// Swan is a type of swimming bird. 

********** compound composition, two premises
<sport --> competition>. %0.90% 
// Sport is a type of competition. 
<chess --> competition>. %0.80%  
// Chess is a type of competition. 
1
 OUT: <(&,chess,sport) --> competition>. %0.98;0.96% 
// If something is both chess and sport, then it is a competition.
 OUT: <(|,chess,sport) --> competition>. %0.72;0.84% 
// If something is either chess or sport, then it is a competition.

********** compound decomposition, two premises
<robin --> (|,bird,swimmer)>.  
// Robin is a type of bird or a type of swimmer. 
<robin --> swimmer>. %0.00% 
// Robin is not a type of swimmer. 
1
 OUT: <robin --> bird>. %1.00;0.81% 
// Robin is a type of bird. 

********** compound decomposition, two premises
<robin --> swimmer>. %0.00%  
// Robin is not a type of swimmer. 
<robin --> (-,mammal,swimmer)>. %0.00%  
// Robin is not a nonswimming mammal. 
1
 OUT: <robin --> mammal>. %0.00;0.81% 
// Robin is not a type of mammal.

********** set operations
<planetX --> {Mars,Pluto,Venus}>. %0.90% 
// PlanetX is a member of the group containing Mars, Pluto and Venus.  
<planetX --> {Pluto,Saturn}>. %0.70% 
// PlanetX is a member of the group containing Pluto and Saturn. 
7
 OUT: <planetX --> {Mars,Pluto,Saturn,Venus}>. %0.97;0.81%
// PlanetX is a member of the group containing Mars, Pluto, Saturn, and Venus. 
 OUT: <planetX --> {Pluto}>. %0.63;0.81% 
// PlanetX is probably Pluto. 

********** composition on both sides of a statement
<bird --> animal>. %0.90% 
// Bird is a type of animal. 
<(&,bird,swimmer) --> (&,animal,swimmer)>?  
// Is a swimming bird a type of swimming animal?
5
 OUT: <(&,bird,swimmer) --> (&,animal,swimmer)>. %0.90;0.73% 
// A swimming bird is probably a type of swimming animal.

********** composition on both sides of a statement
<bird --> animal>. %0.90% 
// Bird is a type of animal. 
<(-,swimmer,animal) --> (-,swimmer,bird)>?   
// Is a nonanimal swimmer a type of a nonbird swimmer?
5
 OUT: <(-,swimmer,animal) --> (-,swimmer,bird)>. %0.90;0.73% 
// A nonanimal swimmer is probably a type of nonbird swimmer.
 
********** compound composition, one premise
<swan --> bird>. %0.90%  
// Swan is a type of bird. 
<swan --> (|,bird,swimmer)>? 
// Is a swan a type of bird or swimmer?
2
 OUT: <swan --> (|,bird,swimmer)>. %0.90;0.81% 
// A swan is probably a type of bird or swimmer. 

********** compound composition, one premise
<swan --> bird>. %0.90% 
// Swan is a type of bird. 
<(&,swan,swimmer) --> bird>?
// Is swimming swan a type of bird?
1
 OUT: <(&,swan,swimmer) --> bird>. %0.90;0.81% 
// Swimming swan is a type of bird.

********** compound composition, one premise
<swan --> bird>. %0.90% 
// Swan is a type of bird. 
<swan --> (-,swimmer,bird)>?  
// Is swan a type of nonbird swimmer?
2
 OUT: <swan --> (-,swimmer,bird)>. %0.10;0.81% 
// A swan is not a type of nonbird swimmer.
 
********** compound composition, one premise
<swan --> bird>. %0.90% 
// Swan is a type of bird. 
<swan --> (~,bird,swimmer)>?  
// Does swan have the property that differs bird from swimmer?
2
 OUT: <swan --> (~,bird,swimmer)>. %0.90;0.81% 
// Swan has the property that differs bird from swimmer.

********** compound decomposition, one premise
<robin --> (&,bird,swimmer)>. %0.90% 
// Robin is a type of swimming bird.
6
 OUT: <robin --> bird>. %0.90;0.81% 
// Robin is a type of bird. 

********** compound decomposition, one premise
<robin --> (-,bird,swimmer)>. %0.90% 
// Robin is a type of nonswimming bird. 
6
 OUT: <robin --> bird>. %0.90;0.81% 
// Robin is a type of bird. 
